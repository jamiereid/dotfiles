#
# Jambulance's theme
#
# Author:
#   Jamie Reid <jamie@jre.id.au>
#

function prompt_jambulance_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="%F{136}(%f${_prompt_jambulance_colors[1]}%b%f%u%c${_prompt_jambulance_colors[4]}●%f%F{136})%f"
  else
    branch_format="%F{136}(%f${_prompt_jambulance_colors[1]}%b%f%u%c%F{136})%f"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

  if (( $+functions[python-info] )); then
    python-info
  fi
  
  _prompt_jambulance_pwd="%{%F{166}%}${${(%):-%~}//\//%F{166\}/%f}%f"
}

function prompt_jambulance_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling things before each command
  add-zsh-hook precmd prompt_jambulance_precmd
  
  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_jambulance_colors=(
      "%F{81}"  # Turquoise
      "%F{166}" # Orange
      "%F{135}" # Purple
      "%F{161}" # Hotpink
      "%F{118}" # Limegreen
    )
  else
    _prompt_jambulance_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi
  
  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="%F{136}(%f${_prompt_jambulance_colors[1]}%b%f%u%c%F{136})%f"
  local action_format="%F{136}(%f${_prompt_jambulance_colors[5]}%a%f%F{136})%f"
  local unstaged_format="${_prompt_jambulance_colors[2]}●%f"
  local staged_format="${_prompt_jambulance_colors[5]}●%f"
  
  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '(%v)'
  
  # Define prompts.
  PROMPT='%F{136}(%f${_prompt_jambulance_pwd}%F{136})%f${vcs_info_msg_0_}$python_info[virtualenv]%F{166}»%f '
  RPROMPT='%F{243}%n@%m%f'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_jambulance_setup "$@"
